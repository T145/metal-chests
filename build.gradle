
import de.undercouch.gradle.tasks.download.Download

buildscript {
	repositories {
		jcenter()
		maven {
			name 'MinecraftForge Repo'
			url 'http://files.minecraftforge.net/maven'
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}

plugins {
	id 'org.ajoberstar.grgit' version '2.1.1'
	id 'de.undercouch.download' version '3.3.0'
}

apply plugin: 'net.minecraftforge.gradle.forge'

task wrapper(type: Wrapper) {
	gradleVersion = gradle_version
}

task setupWorkspace(type: Download, dependsOn: setupDecompWorkspace) {
	def forgeArchive = project.file('forge.zip')

	src 'http://files.minecraftforge.net/maven/net/minecraftforge/forge/' + forge_version + '/forge-' + forge_version + '-mdk.zip'
	dest forgeArchive

	doLast {
		project.copy {
			from zipTree(forgeArchive)
			into 'forge'
		}

		project.file('forge/eclipse').renameTo(project.file('eclipse'))

		project.file('forge.zip').delete()
		project.file('forge').deleteDir()
	}
}

version = (grgit.describe(longDescr: true) - '-0').replaceAll('-', '.')
group = mod_group
archivesBaseName = mod_id

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
	version = forge_version
	mappings = forge_mappings
	runDir = 'run'
	useDepAts = true
	makeObfSourceJar = false

	replaceIn "${mod_id}.java"
	replace "@VERSION@", project.version
}

dependencies {
	compile fileTree(dir: 'libs', include: '*.jar')
}

processResources {
	exclude '**/*.db'
	exclude '**/*.DS_Store'

	// move our access transformer
	rename '(.+_at.cfg)', 'META-INF/$1'
}

build {
	doLast {
		project.file('release').deleteDir()
		project.file('build/libs').renameTo(project.file('release'))
		project.file('build/tmp/recompileMc/sources').renameTo(project.file('minecraftSrc'))
	}
}

jar {
	manifest {
		attributes 'FMLAT': 'metalchests_at.cfg'
	}
}
