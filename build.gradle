import com.palantir.gradle.gitversion.VersionDetails
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        maven { url 'https://files.minecraftforge.net/maven' }
        maven { url 'https://plugins.gradle.org/m2/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
        classpath "com.palantir.gradle.gitversion:gradle-git-version:0.12.3"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.palantir.git-version'

version = gitVersion()
group = 't145.metal-chests'
archivesBaseName = 'Metal Chests'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

task copySource(type: Copy) {
    from 'src/java'
    into "$buildDir/src"
    filter(ReplaceTokens, tokens: [
        '@VERSION@': project.version
    ])
}

compileJava.source "$buildDir/src"
compileJava.dependsOn copySource

minecraft {
    mappings channel: 'snapshot', version: '20180814-1.12'
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }

        server {
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'
}

processResources {
    exclude '**/*.db'
    exclude '**/*.DS_Store'
}

jar {
    manifest {
        attributes([
            "Specification-Title": "metal-chests",
            "Specification-Vendor": "t145",
            "Specification-Version": "1",
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
            "Implementation-Vendor" :"t145",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
